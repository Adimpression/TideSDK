#!/usr/bin/env python

# This file has been modified from its orginal sources.
#
# Copyright (c) 2012 Software in the Public Interest Inc (SPI)
# Copyright (c) 2012 David Pratt
# 
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Copyright (c) 2008-2012 Appcelerator Inc.
# 
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import os, re, sys, inspect, os.path as path
from tide import BuildConfig

debug = False
Import('*')

# this will ensure that you're using the right version of scons
EnsureSConsVersion(1,2,0)
# this will ensure that you're using the right version of python
EnsurePythonVersion(2,5)


build = BuildConfig(
    PRODUCT_VERSION = '0.1',
    PRODUCT_NAME = 'Kroll',
    GLOBAL_NS_VARNAME = 'kroll',
    CONFIG_FILENAME = 'tiapp.xml',
    BUILD_DIR = path.abspath('build'),
    THIRD_PARTY_DIR = path.abspath('thirdparty'),
    DISTRIBUTION_URL = 'api.appcelerator.net',
    CRASH_REPORT_URL = 'api.appcelerator.net/p/v1/app-crash-report',
)
build.set_tide_source_dir(path.abspath('.'))

# This should only be used for accessing various
# scripts in the kroll build directory. All resources
# should instead be built to build.dir
build.tide_build_dir = path.join(build.tide_source_dir, 'build')
build.env.Append(CPPPATH=[
    build.tide_source_dir, 
    build.tide_include_dir])

# debug build flags
debug = ARGUMENTS.get('debug', 0)
if debug:
    build.env.Append(CPPDEFINES = ('DEBUG', 1))
    if build.is_win32():
        build.env.Append(CCFLAGS=['/Z7'])  # max debug
        build.env.Append(CPPDEFINES=('WIN32_CONSOLE', 1))
    else:
        build.env.Append(CPPFLAGS=['-g'])  # debug
else:
    build.env.Append(CPPDEFINES = ('NDEBUG', 1 ))
    if not build.is_win32():
        build.env.Append(CPPFLAGS = ['-O9']) # max optimizations

if build.is_win32():
    build.env.Append(CCFLAGS=['/EHsc', '/GR', '/MD'])
    build.env.Append(LINKFLAGS=['/DEBUG', '/PDB:${TARGET}.pdb'])

Export('build')
SConscript('SConscript.thirdparty', duplicate=0)

if 'docs' in COMMAND_LINE_TARGETS:
    SConscript('SConscript.docs')
else:
    SConscript('SConscript', exports='debug')
