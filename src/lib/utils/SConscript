#!/usr/bin/env python

# This file has been modified from its orginal sources.
#
# Copyright (c) 2012 Software in the Public Interest Inc (SPI)
# Copyright (c) 2012 Mital Vora
# 
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import os
import os.path as path
import effess
Import('build')
Import('LIBUTILS_NAME')

libutils_sources = []
libutils_env = build.env.Clone()
libutils_name = LIBUTILS_NAME

sources = libutils_sources + \
    Glob('*.cpp') + \
    Glob('poco/*.cpp')

if build.is_linux():
    sources += Glob('linux/*.cpp')

if build.is_win32():
    sources += Glob('unzip/*.cpp') + Glob('win/*.cpp')
else:
    sources += Glob('posix/*.cpp')

if build.is_osx():
    sources +=  Glob('osx/*.mm')

env = libutils_env
env.Append(CPPDEFINES=[('TIDE_UTILS_API_EXPORT', 1)])

#if build.is_win32():
#    env.Append(CCFLAGS=['/DUNICODE', '/D_UNICODE'])
#    env.Append(LINKFLAGS=['/LTCG', '/INCREMENTAL:NO'])

if build.is_osx():
    env.Append(LINKFLAGS='-install_name lib' + libutils_name + '.dylib')
    env.Append(FRAMEWORKS=['Cocoa', 'SystemConfiguration', 'CoreServices'])

build.mark_build_target(env.Library(path.join(build.dir, 'runtime', libutils_name), source=sources))

# copy the includes for building the SDK
tide_utils_include = path.join(build.tide_include_dir, libutils_name)
effess.copy_tree(build.cwd(), tide_utils_include, include=['.h'])
